plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
}

apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-parcelize'
apply from: '../config/detekt/detekt.gradle'

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 33

    signingConfigs {
        release {
            storeFile file("$rootDir/store/marvel_app_key_store.jks")
            storePassword 'marvel@123'
            keyAlias 'marvel-app'
            keyPassword 'marvel@123'
        }
    }

    defaultConfig {
        applicationId "com.example.marvelapp"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.example.marvelapp.CustomTestRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']
        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        //desatina as animações do app para o teste ficar mais rapido.
        animationsDisabled = true
    }

    buildTypes {
        /**
         * Para aplicativos profissionais, sempre temos que tr pelo menos essas tres builds
         * Debug - vamos usar ela para trabalhar com o nosso projeto, fazer testes e execuções de forma rapida;
         *  - applicationIdSuffix '.debug' - para eu conseguir identificar a nivel de application id, qual é o build type de release, debug e afins
         *  - debuggable true - para conseguimos colocar os breakPoints
         *
         * Staging - Esse é para dar a ideia de pré produção, uma versão que deve ser testada antes de ir para produção
         * Geralmente, a equipe de QA, os testadores, vão usar essa versão para poder fazer os testes necessarios.
         *  - initWith debug - Colocamos isso por que a release de staging não é debugavel,
         *      com essa configuração fazemos com que o staging emita o funcionamento do debug.
         *      depois, precisamos fazer uma regra no proguard, para ele permitir o debug nessa build de staging
         *      (criamos um outro proguard no modulo app)
         *
         *  - minifyEnabled true - para ofuscar nosso codigo.
         *  - applicationIdSuffix '.staging' - para eu conseguir identificar a nivel de application id, qual é o build type de release, debug e afins
         *
         *  Release -
         *  - minifyEnabled true - para ofuscar nosso codigo.
         *  - shrinkResources true - para otimizar nosso codigo
         * */
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        staging {
            initWith debug
            minifyEnabled true
            applicationIdSuffix '.staging'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation project(':core')

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:29.3.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Material design
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Navigation
    def nav_version = '2.5.2'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewModel and LiveData
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    def coroutines = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Room
    def room_version = "2.4.3"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Paging3
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Other Libs
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "androidx.room:room-testing:$room_version"

    // Instrumentation tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}